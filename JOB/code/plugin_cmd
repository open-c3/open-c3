#!/data/Software/mydan/perl/bin/perl -I/data/Software/mydan/JOB/lib -I/data/Software/mydan/JOB/private/lib
use strict;
use warnings;

use FindBin qw( $RealBin );
use MIME::Base64;
use Data::Dumper;

use MYDan::Util::OptConf;
use MYDan::Agent::Client;
use MYDan::VSSH::Print;
use Digest::MD5;
use Code;
use Logs;
use variable;
use Buildin;


$| ++;

=head1 SYNOPSIS

    db => $mysql,
    uuid => 'uuid',
    taskuuid => 'uuid', # Used to make a pause
    projectid => 1, ? check node info
    fromjob => jobuuid or undef , It is used to determine the subtask, Rely on the taskuuid . and the jobuuid use for variable replace
    variable => hash

    logs => 日志对象

    head => 0 or 1
    tail => 0 or 1

=cut

return sub
{
    my %param = @_;
    my ( $db, $uuid, $taskuuid, $projectid, $fromjob, $variable, $logs, $head, $tail )
        = @param{qw(db uuid taskuuid projectid fromjob variable logs head tail )};

    delete $ENV{MYDanExtractFile};
    delete $ENV{MYDanExtractFileAim};

    $logs = Logs->new( 'code.plugin_cmd' ) unless $logs;
    my $vv = Code->new( 'vv' );

    if( $taskuuid )
    {
        open STDOUT, '>>', "$RealBin/../logs/task/${taskuuid}${uuid}cmd" 
            or $logs->die( "Can't open 'logs/task/${taskuuid}${uuid}cmd': $!" );
        open (STDERR, ">&STDOUT") or $logs->die( "open STDERR failed: $uuid" );
    }

    $logs->die( "uuid format error" ) unless $uuid =~ /^[a-zA-Z0-9]+$/;

    my $x = eval{ $db->query( "select user,node_type,node_cont,scripts_type,scripts_cont,scripts_argv,timeout,pause,deployenv,action,batches
            from openc3_job_plugin_cmd where uuid='$uuid'" );};
    $logs->die( "get plugin_cmd info from mysql fail plugin_cmd uuid=$uuid: $@" ) if $@;
    $logs->die( "get plugin_cmd info from mysql fail plugin_cmd uuid=$uuid" ) unless defined $x && ref $x eq 'ARRAY';
    $logs->die( "plugin_cmd uuid mismatch uuid=$uuid" ) unless @$x;

    my ( $user, $node_type, $node_cont, $scripts_type, $scripts_cont, $scripts_argv, $timeout, $pause, $deployenv, $action, $batches ) = @{$x->[0]};

    my $xx = `c3mc-jobx-task-info-bysubuuid '$taskuuid'`;
    my $xxcc = eval { YAML::XS::Load $xx };
    $logs->die( "get yaml fail from c3mc-jobx-task-substat '$taskuuid': $@" ) if $@;
    $logs->die( "get yaml fail from c3mc-jobx-task-substat '$taskuuid': no HASH" ) unless $xxcc && ref $xxcc eq 'HASH';
    
    print YAML::XS::Dump $xxcc;

    $xxcc->{action}    ||= 'nofind';
    $xxcc->{deployenv} ||= 'nofind';
    $xxcc->{batches}   ||= 0;
    $xxcc->{groups}    ||= 0;
    $xxcc->{submitter} ||= 'nofind';

    print "=" x 35, "\n";
    print "check ...\n";
    print "expect deployenv: $deployenv; real deployenv:$xxcc->{deployenv}\n";
    return 'success' if $deployenv eq 'test'   && $xxcc->{deployenv} ne 'test';
    return 'success' if $deployenv eq 'online' && $xxcc->{deployenv} ne 'online';
      
    print "expect action: $action; real action:$xxcc->{action}\n";
    return 'success' if $action eq 'deploy'   && $xxcc->{action} ne 'deploy';
    return 'success' if $action eq 'rollback' && $xxcc->{action} ne 'rollback';
      
    print "expect batches: $batches; real batches: $xxcc->{batches}\n";
    return 'success' if $batches eq 'firsttime'   && $xxcc->{batches} ne 1;
    return 'success' if $batches eq 'thelasttime' && $xxcc->{batches} ne $xxcc->{groups};
    return 'success' if $batches eq 'notfirsttime'   && $xxcc->{batches} eq 1;
    return 'success' if $batches eq 'notthelasttime' && $xxcc->{batches} eq $xxcc->{groups};

    $logs->die( "user format error" ) unless defined $user && $user =~ /^[a-zA-Z0-9_\-]+$/;

    $logs->die( "taskuuid format error" ) if defined $taskuuid && $taskuuid !~ /^[a-zA-Z0-9]+$/;
    my ( $subtasktimems, $subtaskstarttime ) 
        = ( time, POSIX::strftime( "%Y-%m-%d %H:%M:%S", localtime ) );

    my @node;
    if( $node_type eq 'builtin' )
    {
        @node = split /,/, $node_cont;
    }
    elsif( $node_type eq 'variable' )
    {
        eval { $node_cont = variable->new( variable => $variable, db => $db, jobuuid => $fromjob )->relpace( $node_cont ) if defined $node_cont; };
        $logs->die( "relpace variable node_cont fail:$@" ) if $@;
        @node = split /,/, $node_cont;
    }
    elsif( $node_type eq 'group' )
    {
        $logs->die( "node_cont not a number: plugin_cmd uuid=$uuid" ) 
            unless $node_cont && $node_cont =~ /^\d+$/;
        @node = eval{ Code->new( 'nodegroup' )->run( db => $db, id => $node_cont, logs => $logs );};
        $logs->die( "nodegroup code run fail :$@" ) if $@;
    }else
    {
        $logs->die( "node_type : $node_type unknown" );
    }

    my $openc3skipnode = ( grep{ $_ eq  'openc3skipnode' }@node  ) ? 1 : 0;
    @node = ( 'openc3skipnode' ) if $openc3skipnode;

    if( $taskuuid && $fromjob )
    {
        my $nodecount = @node;
        eval{ $db->execute( 
                "update `openc3_job_subtask` set nodecount='$nodecount',starttime='$subtaskstarttime',runtime='0.00',pause='$pause',status='running'
                     where parent_uuid='$taskuuid' and subtask_type='cmd' and uuid='$uuid'") };
        $logs->die( "set status to subtask fail:$@" ) if $@;
    }

    $logs->die( "scripts_type undef" ) unless $scripts_type;
    if( $scripts_type eq 'cite' )
    {
        $logs->die( "scripts_type no a number" ) unless $scripts_cont && $scripts_cont =~ /^\d+$/;
        my $st = eval{ $db->query( "select type,cont from openc3_job_scripts where id='$scripts_cont' and status='available'" );};
        $logs->die( "get scripts cite fail:$@" ) if $@;
        $logs->die( "get scripts cite fail" ) unless defined $st && ref $st eq 'ARRAY';
        $logs->die( "scripts id mismatch: $scripts_cont" ) unless @$st;
        ( $scripts_type, $scripts_cont ) = @{$st->[0]};
    }

    $scripts_cont = decode_base64( $scripts_cont );
    $scripts_argv = decode_base64( $scripts_argv ) if defined $scripts_argv;


    eval { $scripts_argv = variable->new( variable => $variable, db => $db, jobuuid => $fromjob )->relpace( $scripts_argv ) if defined $scripts_argv; };
    $logs->die( "relpace variable scripts_argv fail:$@" ) if $@;

    my $_exit_;
    eval { $_exit_ = variable->new( variable => $variable, db => $db, jobuuid => $fromjob )->get( '_exit_' ) };
    $logs->die( "get variable _exit_ fail:$@" ) if $@;

    $timeout = 60 unless $timeout && $timeout =~ /^\d+$/;

    my $env = variable->new( variable => $variable, db => $db, jobuuid => $fromjob )->wk( $projectid );

    $MYDan::Util::OptConf::THIS = 'agent';

    my %type =
    (
        shell => '/bin/bash',
        python => '/usr/bin/python',
        perl => '/usr/bin/perl',
        php => '/usr/bin/php',
    );

    my %o = MYDan::Util::OptConf->load()->dump();
    $o{verbose} = 1;
    $o{user} = 'jobsys';
    $o{sudo} = $user;
    $o{timeout} = $timeout;

    $scripts_cont =~ s/\n+$/\n/;

    my %query = ( 
        env => $env,
        code => 'scripts', 
        argv => [ +{ 
                 type => $type{$scripts_type}, 
                 cont => $scripts_cont, 
                 md5 => Digest::MD5->new()->add( $scripts_cont )->hexdigest(),  
                 argv => $scripts_argv } ], 
         map{ $_ => $o{$_} }qw( user sudo )
     );


    my $subtaskstatus = 'success'; #runnigs,fail,success,decision,ignore

    my ( $flowid, $version, $buildininfoget ) = ( 0, 0, 1 );
    my $subtasksrefuse = '';
    my $subtasksfail   = '';
    while(1)
    {
        delete $query{auth};
        my %result;

        my $checkerror;

        if( defined $projectid && ! $openc3skipnode )
        {
            my $nodeinfo_check = Code->new( 'nodeinfo_check' );

            $checkerror = eval{ $nodeinfo_check->run( db => $db, id => $projectid, node => [ grep{ $_ !~ /^f\d+$/ && $_ ne 'fx' && $_ ne 'online.env' && $_ ne 'test.env' }@node ] ) };
            $logs->die( "nodeinfo_check code err:$@" ) if $@;
            print "ERROR: the node not on tree: $checkerror\n" if $checkerror;
        }

      unless( $checkerror )
      {

        if( $scripts_type && $scripts_type eq 'buildin' )
        {
            if( $fromjob && $buildininfoget )
            {
                $buildininfoget = 0;
                $x = $db->query( "select name from openc3_job_jobs where uuid='$fromjob'" );
                $logs->die( "get data error from db" ) unless defined $x && ref $x eq 'ARRAY';
                $logs->die( "job name null: jobuuid='$fromjob'" ) unless @$x;
                $flowid = $1 if $x->[0][0] =~ /^_ci_(\d+)_$/;

                eval { $version = variable->new( variable => $variable, db => $db, jobuuid => $fromjob )->get( 'version' ) };
                $logs->die( "get variable version fail:$@" ) if $@;
                $version = 0 unless $version && $version =~ /^[a-zA-Z0-9\.\-_]+$/;
            }

            print "\n############################## INIT ##############################\n\n";

            %result = Buildin->new(
                @node
            )->run( %o, query => \%query, jobuuid => $fromjob, taskuuid => $taskuuid, db => $db, treeid => $projectid, flowid => $flowid, version => $version, deployenv => $xxcc->{deployenv} );
            $subtasksrefuse = 1 if delete $result{_openc3_sys_pluginrefuse_sys_openc3_};
            $subtasksfail   = 1 if delete $result{_openc3_sys_pluginfail_sys_openc3_  };
        }
        else
        {
            %result = MYDan::Agent::Client->new(
                @node
            )->run( %o, query => \%query );
        }

      }

        my @failed;
        map{ 
            my $exit = $result{$_} && $result{$_} =~ /--- (\d+)\r?\n$/ ? $1 : 1;
            push( @failed, $_ ) if $exit;
        }@node;

        MYDan::VSSH::Print::result( 'rcal' => %result ) unless $openc3skipnode;

        if( defined $projectid )
        {
            for my $node ( sort keys %result )
            {
                my @x = $result{$node} =~ /([a-zA-Z][a-zA-Z0-9_]+):\[([a-z0-9\._\-]+)\]/g;
                next unless @x;
                eval{ $vv->run( db => $db, logs => $logs, projectid => $projectid, node => $node, vv => \@x ) };
                $logs->die( "vv code run fail :$@" ) if $@;
            }
        }

        if( @failed )
        {
            print "failed:\n";
            map{ print "  $_\n"; }@failed;
        }

        unless( $taskuuid && $fromjob )
        {
            $subtaskstatus = @failed ? 'fail' : 'success';
            last;
        }

        last unless @node = @failed;

        eval{ $db->execute( "update `openc3_job_subtask` set status='decision' where parent_uuid='$taskuuid' and subtask_type='cmd' and uuid='$uuid'") };
        eval{ $db->execute( "update `openc3_job_task` set status='waiting',notify='0' where uuid='$taskuuid'") };
        my $tmpstatus;

        if( $_exit_ && ( $_exit_ eq '1' || $_exit_ eq 'cmd' || $_exit_ eq 'true' ) )
        {
            $tmpstatus = 'fail';
            eval{ $db->execute( "update openc3_job_task set reason='stop by sys._exit_' where uuid='$taskuuid' and reason is null" ) };
            $logs->die( "update reason fail" ) if $@;

        }
        else
        {
            while(1)
            {
                $x = $db->query( "select status from openc3_job_subtask where parent_uuid='$taskuuid' and subtask_type='cmd' and uuid='$uuid'" );
                $logs->die( "get data error from db" ) unless defined $x && ref $x eq 'ARRAY';
                $logs->die( "subtask status null: parent_uuid='$taskuuid' and subtask_type='cmd' and uuid='$uuid'" ) unless @$x;
                if( grep{ $x->[0][0] eq $_ }qw( fail ignore running ) )
                {
                    $tmpstatus = $x->[0][0];
                    last;
                }
                sleep 3;
            }
        }

        eval{ $db->execute( "update `openc3_job_task` set status='running',notify='0' where uuid='$taskuuid'") };

        next if $tmpstatus eq 'running';
        $subtaskstatus = $tmpstatus;
        last;
    }

    $subtaskstatus = 'refuse' if $subtasksrefuse && $subtaskstatus eq 'success';
    $subtaskstatus = 'fail'   if $subtasksfail   && $subtaskstatus eq 'success';
    print "substatus = $subtaskstatus\n";

    if( $taskuuid && $fromjob )
    {
         my $subtaskruntime = sprintf "%0.3f", time - $subtasktimems;
         my $subtaskfinishtime = POSIX::strftime( "%Y-%m-%d %H:%M:%S", localtime );
         eval{ $db->execute( "update `openc3_job_subtask` set runtime='$subtaskruntime',finishtime='$subtaskfinishtime',status='$subtaskstatus' 
                 where parent_uuid='$taskuuid' and subtask_type='cmd' and uuid='$uuid'") };
    }

    return $subtaskstatus unless $taskuuid && $fromjob && $pause;

    Code->new( 'pause' )->run( db => $db,  taskuuid => $taskuuid, subtaskuuid => $uuid, subtasktype => 'cmd', logs => $logs );
    return $subtaskstatus;
}
