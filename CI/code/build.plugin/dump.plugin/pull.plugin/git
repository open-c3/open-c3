#!/data/Software/mydan/perl/bin/perl -I/data/Software/mydan/CI/lib -I/data/Software/mydan/CI/private/lib
use strict;
use warnings;
use POSIX;
use Logs;
use FindBin qw( $RealBin );
use Temp;
use MIME::Base64;
use Code;

$| ++;

=head1 SYNOPSIS

    db => $mysql,
    logs => 日志对象

    # projectid addr ticketid rely name(版本信息可能为空) temp(logs目录下的临时目录名) uuid

=cut

return sub
{
    my %param = @_;

    my ( $db, $logs, $projectid, $addr, $ticketid, $rely, $name, $temp, $uuid )
        = @param{qw(db logs projectid addr ticketid rely name temp uuid )};

    $logs = Logs->new( 'code.build.dump.pull.git' ) unless $logs;

    my $ssh_key;

    if( $ticketid )
    {
        my $x = eval{ $db->query( "select ticket from openc3_ci_ticket where id='$ticketid' and type='SSHKey'" )};  
        $logs->die( "get data from ticket fail:$@" ) if $@;
        $ssh_key = $x->[0][0] if @$x;
    }

    my $ctrl = $ssh_key ? "$RealBin/../bin/git -i " . Temp->new( chmod => 0600 )->dump( $ssh_key ) : 'git';

    my $temppath = "$RealBin/../logs/$temp/$uuid";
    if( -d $temppath )
    {
        my $user = `id -un`;chop $user;
        my $sudo = $user eq 'root' ? '' : 'sudo ';
        $logs->die( "clean old temppath fail:$!" ) if system "${sudo}rm -rf '$temppath'";
    }

    $logs->die( "mkdir temppath fail:$!" ) if system "mkdir -p '$temppath'";


    my ( $dumptags, $version ) = ( '', 'release' );
    $name = '' unless defined $name;
    if( $name )
    {
        $logs->die( "name format error" ) unless $name =~ /^[a-zA-Z0-9\.\-_]+$/;
        ( $dumptags, $version ) = ( "-b $name", $name );
    }

    my $tagger;
    my $tag_info = '';
    if( $name =~ /^release-testonly-[0-9a-z]{40}/ )
    {
        my ( $commitid, $event_name, $branch );

        if( $name =~ /^release-testonly-([0-9a-z]{40})_([01]{1})-([a-zA-Z0-9][a-zA-Z0-9\-\._])$/ )
        {
            ( $commitid, $event_name, $branch ) = ( $1, $2, $3 );
            $event_name = $event_name ? 'create' : 'delete';
        }
        elsif( $name =~ /^release-testonly-([0-9a-z]{40})-([a-zA-Z0-9][a-zA-Z0-9\-\._]+)$/ )
        {
            ( $commitid, $event_name, $branch ) = ( $1, 'push', $2 );
        }
        elsif( $name =~ /^release-testonly-([0-9a-z]{40})$/ )
        {
            ( $commitid, $event_name, $branch ) = ( $1, 'push', 'master' );
        }
        else
        {
            $logs->die( "name format error" );
        }

        $branch =~ s#\._\.#/#g;

        if( $event_name eq 'delete' )
        {
            $logs->die( "clone error:$!" )
                if system "cd '$temppath' && rm -rf data data.tmp info && mkdir data && mkdir info && echo '$version' > info/version && echo '$branch' > info/branch && echo '$event_name' > info/event_name";
        }
        else
        {
            $logs->die( "clone error:$!" )
                if system "cd '$temppath' && rm -rf data data.tmp info && $ctrl clone --depth 1 --progress -b $branch --recursive $addr data.tmp && mkdir -p info && cd data.tmp && git reset --hard $commitid && cd .. && mv data.tmp/.git info/ && mv data.tmp data && echo '$version' > info/version && echo '$branch' > info/branch && echo '$event_name' > info/event_name";
        }

        my $t = eval{ $db->query( "select `tagger` from openc3_ci_version where projectid='$projectid' and name='$name'" )};  
        $logs->die( "get tagger from version  fail:$@" ) if $@;
        $tagger = $t->[0][0] if $t && ref $t && ref $t->[0];
        $tag_info = `cd '$temppath/info' && $ctrl show -s --format="%s"`;
    }
    else
    {
        
        my @cacheid = `cat /etc/ci.cache 2>/dev/null`;
        chomp @cacheid;
        if( grep{ $projectid eq $_ }@cacheid )
        {
            print "get git data from cache\n";
            eval{ Code->new( "build.plugin/dump.plugin/gitclonebycache" )->run( ctrl => $ctrl, addr => $addr, tag => $name, logs => $logs, topath => $temppath ); };
            $logs->die( "run code.build.dump.gitclonebycache fail:$@" ) if $@;
            $logs->die( "echo version error:$!" ) if system "echo '$version' > $temppath/info/version";
        }
        else
        {

            $logs->die( "clone error:$!" )
                if system "cd '$temppath' && rm -rf data data.tmp info && $ctrl clone --depth 1 --progress $dumptags --recursive $addr data.tmp && mkdir -p info && mv data.tmp/.git info/ && mv data.tmp data && echo '$version' > info/version";

        }

        my $info = `cd '$temppath/info' && $ctrl show  -s --format="%ce" $name`;
        $tag_info = `cd '$temppath/info' && $ctrl show -s --format="%s" $name`;

        Encode::_utf8_off($info);
        print "git info: $info\n";

        my @m;
        if( $info =~ /deploy\s*:\s*([\w\.]+\@[a-zA-Z0-9]+\.com)/ )
        {
            @m = ( $1 );
        }
        elsif( $info =~ /<([\w\.]+\@[a-zA-Z0-9]+\.[a-z]+)>/ )
        {
            @m = ( $1 );
        }
        else
        {
            @m = $info =~ /[\w\.\@]+\.com/g;
            unless( @m )
            {
                my @info = grep{ $_ =~ /^[\w\.\@]+$/ }split /\n/, $info;
                push @m, $info[-1] if $info[-1];
            }
        }
        $tagger = $m[0];
        eval{ $db->execute( "update openc3_ci_version set tagger='$tagger' where projectid='$projectid' and name='$name'" ) };
        print "WARN: error recording tagger $@\n" if $@;
    }

    unless( defined $tagger && $tagger =~ /^[a-zA-Z0-9\.\@\-_]+$/ )
    {
        my $cidefaulttagger = `c3mc-sys-ctl cidefaulttagger`;
        chomp $cidefaulttagger;
        print "get tagger fail,Switch to default: $cidefaulttagger\n";
        $tagger = $cidefaulttagger;
    }

    $tag_info =~ s/'//g;
    $logs->die( "write tagger info fail:$!" )if system "echo '$tagger' > '$temppath/info/tagger'";
    $logs->die( "write tag_info info fail:$!" )if system "echo '$tag_info' > '$temppath/info/taginfo'";

    eval{ $db->execute( "update openc3_ci_version set taginfo='$tag_info' where projectid='$projectid' and name='$name'" ) };
    print "WARN: error recording taginfo $@\n" if $@;
}
