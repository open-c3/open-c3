#!/data/Software/mydan/perl/bin/perl -I/data/Software/mydan/Connector/lib
use strict;
use warnings;

binmode STDIN, ':utf8';
binmode STDOUT, ':utf8';
binmode STDERR, ':utf8';

$|++;

use MYDB;
use MYDan::Util::OptConf;
use LWP::UserAgent;
use JSON;
use Configini;
use OPENC3::SysCtl;

=head1 SYNOPSIS

 $0 col1 col2 .. col2 [ --col name,inip [id|name|type|inip|exip|projectid] ]

=cut

my $option = MYDan::Util::OptConf->load();
my %o = $option->set( col => 'name,inip' )->get( qw( col=s ) )->dump();

my @col = split /,/, $o{col};

my @group;
map{
    my $id = $_;
    my %g;
    $g{nodeinfo} = Configini::get( "nodeinfo$id" );
    my %env = Configini::env( "nodeinfoenv$id" );
    $g{env} = %env ? \%env : +{};

    $g{nodeinfoeid} = Configini::get( "nodeinfoeid$id" );
    $g{nodeinfoeid} = undef unless $g{nodeinfoeid} && ref $g{nodeinfoeid} eq 'ARRAY' && @{$g{nodeinfoeid}} == 2;

    push @group, \%g if $g{nodeinfo};
}( '', 1 .. 9 );
 
my $db = MYDB->new( "/data/Software/mydan/Connector/conf/conn", delayedconnection => 1 );

sub getnodeinfo
{
    my ( %param )= @_;
    my ( $id, $nodeinfo, $env ) = @param{qw(id nodeinfo env )};
    die unless $id =~ /^\d+$/;

    if( $nodeinfo =~ /api.connector.open-c3.org/ )
    {
        my $x = eval{ $db->query( sprintf "select $o{col} from openc3_connector_nodelist where projectid=$id and status='available'" ); };
        die $@ if $@;
        map{ printf "%s\n", join ';', @$_ }@$x;
        return;
    }

    my $cache     = "/data/open-c3-data/cache/nodeinfo/nodeinfo-$id";
    my $cachetime =  OPENC3::SysCtl->new()->getint( 'sys.cache.nodeinfo.timeout', 0, 86400 * 365, 60 );

    $cachetime = 604800 if -f '/var/openc3_connector_api_fail';

    if( $cachetime && -f $cache )
    {
        my $ctime = ( stat $cache )[9]; #mtime
        if( $ctime + $cachetime > time )
        {
            my $v;
            eval{
                $v = eval{ YAML::XS::LoadFile $cache };
                die "get cache fail: $@" if $@;
                die "stat no true"   unless $v->{stat};
                die "data no ARRAY"  unless $v->{data} && ref $v->{data} eq 'ARRAY';
            };

            if( $@ )
            {
                warn "get cache fail: $@";
            }
            else
            {
                for my $t ( @{$v->{data}} )
                {
                    $t->{projectid} = $id;
                    printf "%s\n", join ';', map{ $t->{$_} || '' }@col;
                }
                return;
            }
        }
    }

    my $ua = LWP::UserAgent->new;
    $ua->timeout( 15 );
    $ua->default_header( %$env ) if $env;

    my ( $res, $err );
    for my $i ( reverse 1 .. 3 )
    {
        $res = $ua->get( "${nodeinfo}$id" );
        $err = sprintf "get nodeinfo fail: ${nodeinfo}$id: %s", YAML::XS::Dump $res;
        $err =~ s/appkey:\s+['"]?[a-zA-Z0-9]{32}['"]?\b/appkey: ******/g;
        last if $res->is_success;
        if( $i > 1 )
        {
            warn sprintf( "$err code %s", $res->code );
            sleep 3;
        }
    }

    my $v;
    eval{
        die sprintf( "code %s", $res->code ) unless $res->is_success;

        $v = eval{JSON::decode_json $res->decoded_content};

        die "data no JSON: $@"  if $@;
        die "stat no true"  unless $v->{stat};
        die "data no ARRAY" unless $v->{data} && ref $v->{data} eq 'ARRAY';
    };

    if( $@ )
    {
        warn "$err $@";
        #TODO if cache too old ?
        $v = eval{ YAML::XS::LoadFile $cache };
        die "get cache fail: $@" if $@;
        die "stat no true"   unless $v->{stat};
        die "data no ARRAY"  unless $v->{data} && ref $v->{data} eq 'ARRAY';
    }
    else
    {
        if( $cachetime )
        {
            eval{
                YAML::XS::DumpFile "$cache.tmp.$$", $v;
                die "rename fail" unless rename "$cache.tmp.$$", $cache;
            };
            warn "set cache fail: $@" if $@;
        }
    }

    for my $t ( @{$v->{data}} )
    {
        $t->{projectid} = $id;
        printf "%s\n", join ';', map{ $t->{$_} || '' }@col;
    }
}

sub getconfig
{
    my $id = shift @_;
    for my $g ( @group )
    {
        return $g if $g->{nodeinfoeid} && $g->{nodeinfoeid}[0] <= $id && $id <= $g->{nodeinfoeid}[1];
    }
    return $group[0];
}

sub nodeinfo
{
    my $id = shift @_;
    die unless $id =~ /^\d+$/;
    my $config = getconfig( $id );
    getnodeinfo( id => $id, %$config );
}

if( @ARGV ) { map{ nodeinfo( $_ ) }@ARGV; } else { while(<STDIN>){ chomp; nodeinfo($_); } }
